/******************************************************************************
 * Copyright 2019 NVIDIA Corporation. All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *  * Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 *  * Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *  * Neither the name of NVIDIA CORPORATION nor the names of its
 *    contributors may be used to endorse or promote products derived
 *    from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS ``AS IS'' AND ANY
 * EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT OWNER OR
 * CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 * EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
 * PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
 * OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *****************************************************************************/

mdl 1.4;

import anno::*;
import base::*;
import df::*;
import math::*;
import state::*;
import tex::*;

export enum gltf_alpha_mode {
    opaque,
    mask,
    blend
};

float compute_opacity(gltf_alpha_mode mode,  float alpha,  float cutoff) {
    switch (mode) {
        case mask:
            return alpha < cutoff ? 0.0 : 1.0;
        case blend:
            return alpha;
        case opaque:
            return 1.0;
    }
}

float4 gltf_prepare_tex_float4(uniform texture_2d texture, int tex_coord_index) {
    float3 tex_coord = state::texture_coordinate(tex_coord_index);
    return tex::texture_isvalid(texture)
        ? tex::lookup_float4(texture, float2(tex_coord.x, tex_coord.y))
        : float4(1.0);
}

float3 gltf_prepare_tex_float3(uniform texture_2d texture, int tex_coord_index) {
    float3 tex_coord = state::texture_coordinate(tex_coord_index);
    return tex::texture_isvalid(texture)
        ? tex::lookup_float3(texture, float2(tex_coord.x, tex_coord.y))
        : float3(1.0);
}

float3 gltf_prepare_normal(uniform texture_2d texture, int tex_coord_index, int tex_tangent_index, uniform float scale_factor) {
    base::texture_coordinate_info coordinate_info = base::texture_coordinate_info(
        position: state::texture_coordinate(tex_coord_index),
        tangent_u: state::texture_tangent_u(tex_tangent_index),
        tangent_v: state::texture_tangent_v(tex_tangent_index));
    return tex::texture_isvalid(texture)
            ? base::tangent_space_normal_texture(
                texture: texture,
                uvw: coordinate_info,
                factor: scale_factor)
            : state::normal();
}

float gltf_prepare_occlusion(uniform texture_2d texture, int coord_index, float occlusion_strength) {
    return math::lerp(1.0, gltf_prepare_tex_float3(texture, coord_index).x, occlusion_strength);
}

color gltf_perpare_emission(uniform texture_2d texture, int coord_index, color emissive_factor) {
    return emissive_factor * color(gltf_prepare_tex_float3(texture, coord_index));
}

export material gltf_material(
    // base color
    uniform texture_2d base_color_texture = texture_2d(),
    uniform int base_color_texture_coord_index = 0,
    uniform color base_color_factor = color(1.0), 

    // metallic roughness
    uniform texture_2d metallic_roughness_texture = texture_2d(),
    uniform int metallic_roughness_texture_coord_index = 0,
    uniform float metallic_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    uniform float roughness_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // normal map
    uniform texture_2d normal_texture = texture_2d(),
    uniform int normal_texture_coord_index = 0,
    uniform int normal_texture_tangent_index = 0,
    uniform float normal_scale_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // emission
    uniform texture_2d emissive_texture = texture_2d(),
    uniform int emissive_texture_coord_index = 0,
    uniform color emissive_factor = color(1.0),

    // ambient occlusion
    uniform texture_2d occlusion_texture = texture_2d(),
    uniform int occlusion_texture_coord_index = 0,
    uniform float occlusion_strength = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // alpha
    uniform float base_alpha = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // alpha for gltf_alpha_mode::mask
    uniform float alpha_cutoff  = 0.5
    [[
        anno::hard_range(0.0, 1.0)
    ]],
    uniform gltf_alpha_mode alpha_mode = opaque
) [[
    anno::author("NVIDIA Corporation"),
    anno::display_name("GLTF Material"),
    anno::hidden(),
    anno::description("Metallic-Roughness Material that takes the GLTF pbr model parameters as input.")
]] = let {

    // handling GLTF parameters
    // ------------------------------------------------------------------------
    float4 base_float4 = gltf_prepare_tex_float4(base_color_texture, base_color_texture_coord_index);
    color base_color = base_color_factor * color(base_float4.x, base_float4.y, base_float4.z);
    float alpha = base_alpha * base_float4.w;

    float3 metallic_roughness = gltf_prepare_tex_float3(metallic_roughness_texture, metallic_roughness_texture_coord_index);
    float metallic = metallic_factor * metallic_roughness.x;
    float roughness = roughness_factor * metallic_roughness.y;

    float3 normal = gltf_prepare_normal(normal_texture, normal_texture_coord_index, normal_texture_tangent_index, normal_scale_factor);
    float occlusion = gltf_prepare_occlusion(occlusion_texture, occlusion_texture_coord_index, occlusion_strength);
    color emissive_color = gltf_perpare_emission(emissive_texture, emissive_texture_coord_index, emissive_factor);

    // metallic roughness model
    // ------------------------------------------------------------------------

    // compute final roughness by squaring the "roughness" parameter 
    float roughness2 = roughness * roughness;

    // reduce the reflectivity at grazing angles to avoid "dark edges" 
    // for high roughness due to the layering
    float grazing_refl = math::max((1.0 - roughness), 0.0);

    // for the dielectric component we layer the glossy component on top of the diffuse one,
    // the glossy layer has no color tint
    bsdf dielectric_component = df::custom_curve_layer(
        weight: grazing_refl, // roughness is used to weight between diffuse and glossy
        normal_reflectivity: 0.08,
        grazing_reflectivity: grazing_refl,
        layer: df::microfacet_ggx_vcavities_bsdf(roughness_u: roughness2),
        base: df::diffuse_reflection_bsdf(tint: base_color * occlusion)
        );

    // the metallic component doesn't have a diffuse component, 
    // it's only glossy base_color is applied to tint it
    bsdf metallic_component = df::microfacet_ggx_vcavities_bsdf(
        tint: base_color * occlusion, 
        roughness_u: roughness2);

    // final BSDF is a linear blend between dielectric and metallic component
    bsdf dielectric_metal_mix = df::normalized_mix(
        components:
            df::bsdf_component[](
                df::bsdf_component(
                    component: metallic_component,
                    weight: metallic),
                df::bsdf_component(
                    component: dielectric_component,
                    weight: 1.0-metallic)
            )
        );

} in material(
    surface: material_surface(
        scattering: dielectric_metal_mix,
        emission:  material_emission (
            emission: df::diffuse_edf(),
            intensity: emissive_color
        )
    ),
    geometry: material_geometry(
        normal: normal,
        cutout_opacity: compute_opacity(alpha_mode, alpha, alpha_cutoff) 
    )
);


export material gltf_material_khr_specular_glossiness(
    // diffuse color
    uniform texture_2d diffuse_texture = texture_2d(),
    uniform int diffuse_texture_coord_index = 0,
    uniform color diffuse_factor = color(1.0), 

    // specular glossiness
    uniform texture_2d specular_glossiness_texture = texture_2d(),
    uniform int specular_glossiness_texture_coord_index = 0,
    uniform color specular_factor = color(1.0)
    [[
        anno::hard_range(color(0.0), color(1.0))
    ]],

    uniform float glossiness_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // normal map
    uniform texture_2d normal_texture = texture_2d(),
    uniform int normal_texture_coord_index = 0,
    uniform int normal_texture_tangent_index = 0,
    uniform float normal_scale_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // emission
    uniform texture_2d emissive_texture = texture_2d(),
    uniform int emissive_texture_coord_index = 0,
    uniform color emissive_factor = color(1.0),

    // ambient occlusion
    uniform texture_2d occlusion_texture = texture_2d(),
    uniform int occlusion_texture_coord_index = 0,
    uniform float occlusion_strength = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // alpha
    uniform float base_alpha = 1.0
    [[
        anno::hard_range(0.0, 1.0)
    ]],

    // alpha for gltf_alpha_mode::mask
    uniform float alpha_cutoff  = 0.5
    [[
        anno::hard_range(0.0, 1.0)
    ]],
    uniform gltf_alpha_mode alpha_mode = opaque
) [[
    anno::author("NVIDIA Corporation"),
    anno::display_name("GLTF Material"),
    anno::hidden(),
    anno::description("Specular-Glossiness Material that takes the GLTF KHR extension parameters as input.")
]] = let {

    // handling GLTF parameters
    // ------------------------------------------------------------------------
    float4 diffuse_float4 = gltf_prepare_tex_float4(diffuse_texture, diffuse_texture_coord_index);
    color diffuse = diffuse_factor * color(diffuse_float4.x, diffuse_float4.y, diffuse_float4.z);
    float alpha = base_alpha * diffuse_float4.w;

    float4 specular_glossiness = gltf_prepare_tex_float4(specular_glossiness_texture, specular_glossiness_texture_coord_index);
    color specular = specular_factor * color(specular_glossiness.x, specular_glossiness.y, specular_glossiness.z);
    float glossiness = glossiness_factor * specular_glossiness.w;

    float3 normal = gltf_prepare_normal(normal_texture, normal_texture_coord_index, normal_texture_tangent_index, normal_scale_factor);
    float occlusion = gltf_prepare_occlusion(occlusion_texture, occlusion_texture_coord_index, occlusion_strength);
    color emissive_color = gltf_perpare_emission(emissive_texture, emissive_texture_coord_index, emissive_factor);

    // specular glossiness model
    // ------------------------------------------------------------------------

    //color c_diff = diffuse * (1.0 - math::maxvalue(specular));
    float F_0 = math::max_value(specular);
    color F_0_tint = F_0==0.0? color(0.0) : (specular/F_0); 

    float a = (1.0 - glossiness) * (1.0 - glossiness);

	// diffuse BRDF
	bsdf diffuse_brdf = df::diffuse_reflection_bsdf(
        tint: diffuse * occlusion, 
        roughness: 0.0);

	// specular BRDF
	bsdf specular_brdf = df::microfacet_ggx_smith_bsdf(
        tint: color(1.0) * occlusion,
	    roughness_u: a, 
        roughness_v: a, 
        tangent_u: state::texture_tangent_u(0));
	bsdf tinted_specular =  df::directional_factor(normal_tint: F_0_tint, grazing_tint: color(1.0), exponent: 5.0, base: specular_brdf);
	//- Custom curve for specular/diffuse angular mix
	bsdf diffuse_specular_mix = df::custom_curve_layer(
        normal_reflectivity: F_0, 
        grazing_reflectivity: 1.0, 
        exponent: 5.0, 
		weight: 1.0, 
        layer: tinted_specular, 
        base: diffuse_brdf, 
        normal: normal);

} in material(
    surface: material_surface(
        scattering: diffuse_specular_mix,
        emission:  material_emission (
            emission: df::diffuse_edf(),
            intensity: emissive_color
        )
    ),
    geometry: material_geometry(
        normal: normal,
        cutout_opacity: compute_opacity(alpha_mode, alpha, alpha_cutoff) 
    )
);
